# -----------------------------------------------------------------------------
# LifeGame : Conway's Game of Life
#            http://en.wikipedia.org/wiki/Conway's_Game_of_Life
#
# Description :
# - LifeGame is based on 2-state mineral agents: 'live' and 'dead'
# - 'live' agents generate a 'life' field on their 8 neighbouring cells
# - 'dead' and 'live' agents have a 'neighbour' sensor for this 'life' field
# -----------------------------------------------------------------------------

world 17 17 #FF0

mineral dead #FFF
var neighbour
status neighbour < 3 dead   # status lines are evaluated in given order
status neighbour > 2 live   # 'dead' -> 'live' only when 'neighbour' == 3
status neighbour > 3 dead
sensor neighbour life 1

mineral live #000
var life 2
var neighbour
status neighbour < 2 dead
status neighbour > 1 live   # 'live' -> 'live' only when 'neighbour' == 2 or 3
status neighbour > 3 dead
field life -1
sensor neighbour life 1

# initial configuration for the pentadecathlon oscillator (period 15)
agent live (8,3) (8,4) (7,5) (9,5) (8,6) (8,7) (8,8) (8,9) (7,10) (9,10) (8,11) (8,12)
agent dead (0:17,0:3) (0:8,3:5) (9:17,3:5) (0:7,5) (8,5) (10:17,5) (0:8,6:10) (9:17,6:10) (0:7,10) (8,10) (10:17,10) (0:8,11:13) (9:17,11:13) (0:17,13:17)